apply plugin: 'com.android.application'
apply plugin: 'com.github.triplet.play'

def versionMajor = 1
def versionMinor = 0
def versionPatch = 0
def versionBuild = 5
def versionSuffix = versionBuild > 0 ? "-beta" : ""

ext {
    KEY_PATH = System.getenv('TRIP_KEY_PATH')
    STORE_PASSWORD = System.getenv('TRIP_STORE_PASSWORD')
    KEY_ALIAS = System.getenv('TRIP_KEY_ALIAS')
    KEY_PASSWORD = System.getenv('TRIP_KEY_PASSWORD')
    GSTORE_KEY_PATH = System.getenv('GSTORE_KEY_PATH')
}

android {
    compileSdkVersion 28

    playAccountConfigs {
        defaultAccountConfig {
            serviceAccountEmail = "gps-logger-gplay@api-7194191596450161697-308117.iam.gserviceaccount.com"
            jsonFile = file(GSTORE_KEY_PATH)
        }
    }

    defaultConfig {
        applicationId "com.antonchaynikov.triptracker"
        playAccountConfig = playAccountConfigs.defaultAccountConfig
        minSdkVersion 19
        targetSdkVersion 28
        versionCode versionMajor * 100000 + versionMinor * 10000 + versionPatch * 1000 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}.${versionBuild}${versionSuffix}"
        setProperty("archivesBaseName", "$rootProject.name-v$versionName")
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        config {
            storeFile file(KEY_PATH)
            storePassword STORE_PASSWORD
            keyAlias KEY_ALIAS
            keyPassword KEY_PASSWORD
        }
        release {
            storeFile file(KEY_PATH)
            storePassword STORE_PASSWORD
            keyAlias KEY_ALIAS
            keyPassword KEY_PASSWORD
        }
    }

    flavorDimensions "basic"
    productFlavors {
        platform {
            applicationIdSuffix ".platform"
        }
        servicesApi {
            applicationIdSuffix ".servicesApi"
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            multiDexEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

play {
    track = 'alpha'
}

ext {
    ANDROIDX_APPCOMPAT = '1.0.2'
    ANDROIDX_SUPPORT_V4 =  '1.0.0'
    CONSTRAINT_LAYOUT = '2.0.0-alpha2'
    RX_JAVA = '2.1.16'
    RX_ANDROID = '2.1.0'
    GOOGLE_MAPS_SDK = '16.0.0'
    GOOGLE_PLAY_SERVICES_BASE = '16.0.1'
    FIREBASE_CORE = '16.0.5'
    FIREBASE_UI_AUTH = '4.2.1'
    MOCKITO_CORE = '2.22.0'
    JUNIT = '4.12'
    ANDROIDX_TEST_RUNNER = '1.1.0'
    ESPRESSO_CORE = '3.1.0'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "androidx.appcompat:appcompat:$ANDROIDX_APPCOMPAT"
    implementation "androidx.legacy:legacy-support-v4:$ANDROIDX_SUPPORT_V4"
    implementation "androidx.constraintlayout:constraintlayout:$CONSTRAINT_LAYOUT"
    implementation "io.reactivex.rxjava2:rxjava:$RX_JAVA"
    implementation "io.reactivex.rxjava2:rxandroid:$RX_ANDROID"
    implementation "com.google.android.gms:play-services-maps:$GOOGLE_MAPS_SDK"
    implementation "com.google.firebase:firebase-core:$FIREBASE_CORE"
    implementation "com.firebaseui:firebase-ui-auth:$FIREBASE_UI_AUTH"
    implementation "com.google.android.gms:play-services-base:$GOOGLE_PLAY_SERVICES_BASE"

    testImplementation "org.mockito:mockito-core:$MOCKITO_CORE"
    testImplementation "junit:junit:$JUNIT"
    androidTestImplementation "androidx.test:runner:$ANDROIDX_TEST_RUNNER"
    androidTestImplementation "androidx.test.espresso:espresso-core:$ESPRESSO_CORE"
}

apply plugin: 'com.google.gms.google-services'
