apply plugin: 'com.android.application'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-android'
apply plugin: 'jacoco-android'
apply from: '../checkstyle/checkstyle.gradle'
apply plugin: 'io.fabric'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.antonchaynikov.oneclickpublish'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()

keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

oneClickPublish {
    branchNames = ["master"]
    remoteRepoName = "origin"
}

jacoco {
    toolVersion = "0.8.0"
}

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "com.antonchaynikov.triptracker"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode appVersionCode.toInteger()
        versionName appVersionName
        setProperty("archivesBaseName", "$rootProject.name-v$versionName")
        testInstrumentationRunner 'com.antonchaynikov.DexOpenerRunner'
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    flavorDimensions "LocationServiceType"
    productFlavors {
        platform {
            dimension "LocationServiceType"
            applicationIdSuffix ".platform"
        }
        servicesApi {
            dimension "LocationServiceType"
            applicationIdSuffix ".servicesApi"
        }
    }

    buildTypes {
        debug {
            testCoverageEnabled true
            minifyEnabled false
            multiDexEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            multiDexEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        unitTests {
            returnDefaultValues = true
        }
    }

    kapt {
        generateStubs = true
    }

    lintOptions {
        lintConfig file("../lintConfig.xml")
        warningsAsErrors true
    }

    useLibrary 'android.test.runner'
    useLibrary 'android.test.base'
    useLibrary 'android.test.mock'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "androidx.appcompat:appcompat:$ANDROIDX_APPCOMPAT"
    implementation "androidx.legacy:legacy-support-v4:$ANDROIDX_SUPPORT_V4"

    implementation "io.reactivex.rxjava2:rxjava:$RX_JAVA"
    implementation "io.reactivex.rxjava2:rxandroid:$RX_ANDROID"
    implementation "com.google.android.gms:play-services-maps:$GOOGLE_MAPS_SDK"
    implementation "com.google.firebase:firebase-core:$FIREBASE_CORE"
    implementation "com.firebaseui:firebase-ui-auth:$FIREBASE_UI_AUTH"
    implementation "com.google.android.gms:play-services-base:$GOOGLE_PLAY_SERVICES_BASE"
    implementation "com.google.android.gms:play-services-location:$GOOGLE_PALY_SERVICES_LOCATION"
    implementation "com.google.firebase:firebase-firestore:$FIREBASE_FIRESTORE"
    implementation "joda-time:joda-time:$JODA_TIME"
    implementation "org.joda:joda-convert:$JODA_CONVERT:classic"
    implementation "com.crashlytics.sdk.android:crashlytics:$CRASHLYTICS"
    implementation "com.google.dagger:dagger:$DAGGER"
    implementation "androidx.test.espresso:espresso-idling-resource:$ESPRESSO_IDLING_RESOURCE"
    implementation "androidx.navigation:navigation-fragment:$NAV_COMPONENTS"
    implementation "androidx.navigation:navigation-ui:$NAV_COMPONENTS"
    implementation "com.android.support:multidex:$MULTIDEX"
    implementation "com.google.dagger:dagger-android-support:$DAGGER"


    implementation project(path: ':core')
    implementation project(path: ':login')
    implementation project(path: ':tripscreen')
    implementation project(path: ':tripshistory')
    implementation project(path: ':tripslist')

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$KOTLIN_VERSION"

    debugImplementation "androidx.fragment:fragment-testing:$FRAGMENT_TESTING"

    kapt "com.google.dagger:dagger-compiler:$DAGGER"
    kapt "com.google.dagger:dagger-android-processor:$DAGGER"

    testImplementation "org.mockito:mockito-core:$MOCKITO_CORE"
    testImplementation "junit:junit:$JUNIT"
    testImplementation "org.mockito:mockito-inline:$MOCKITO_INLINE"
    testImplementation "com.github.fabioCollini.daggermock:daggermock:$DAGGER_MOCK"

    androidTestImplementation "com.github.fabioCollini.daggermock:daggermock:$DAGGER_MOCK"
    androidTestImplementation "com.github.fabioCollini.daggermock:daggermock-kotlin:$DAGGER_MOCK"
    androidTestImplementation "org.mockito:mockito-core:$MOCKITO_CORE"
    androidTestImplementation "org.mockito:mockito-android:$MOCKITO_ANDROID"
    androidTestImplementation "androidx.test:core:$ANDROID_TEST_CORE"
    androidTestImplementation "androidx.test:rules:$ANDROID_TEST_RULES"
    androidTestImplementation "androidx.test:runner:$ANDROIDX_TEST_RUNNER"
    androidTestImplementation "androidx.test.espresso:espresso-core:$ESPRESSO_CORE"

    androidTestImplementation "com.github.tmurakami:dexopener:$DEX_OPENER"
}

apply plugin: 'com.google.gms.google-services'
